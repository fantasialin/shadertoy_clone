// The MIT License
// Copyright Â© 2021 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

// Analytically finding the discontinuties of the
// gradient of an triangle SDF.
//
// They all emanate from a central point, the
// point that is equally distant from each edge of
// the triangle. I derived the formula by computing
// the bisectors of each angle then finding their
// intersection. Two hours of simplifications later
// it all reduced itself.
//
// Then I discovered this is also called the "incenter"
// and had already a known formula. Oh well, at least
// I can be proud I landed in the right one by myself.

// Other Gradient boundaries:
//
// Triangle: https://www.shadertoy.com/view/tlKcDz
// Quad:     https://www.shadertoy.com/view/WlycWh

// https://www.shadertoy.com/view/tlKcDz
#version 330 core
out vec4 fragColor;

uniform vec3 iResolution;   // viewport resolution (in pixels)
uniform float iTime;        // shader playback time (in seconds)
uniform vec4 iMouse;        // xy: current (if MLB down), zw: click

vec2 triangleIncenter( in vec2 v0, in vec2 v1, in vec2 v2 )
{
    float l0 = length(v2-v1);
    float l1 = length(v0-v2);
    float l2 = length(v1-v0);

    return (v0*l0+v1*l1+v2*l2)/(l0+l1+l2);
}


//=====================================================

// signed distance to a disk
float sdDisk( in vec2 p, in vec2 c, in float r )
{
    return length(p-c)-r;
}

// distance to a line segment
float sdSegment( in vec2 p, in vec2 a, in vec2 b )
{
	vec2 pa = p - a;
	vec2 ba = b - a;
	float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
	return length( pa - ba*h );
}

// signed distance to a 2D triangle
float cro(in vec2 a, in vec2 b ) { return a.x*b.y-a.y*b.x; }
float dot2( in vec2 a ) { return dot(a,a); }
vec3 sdgTriangle( in vec2 p, in vec2 v0, in vec2 v1, in vec2 v2 )
{
    float gs = cro(v0-v2,v1-v0);
    vec4 res;
    
    // edge 0
    {
    vec2  e = v1-v0;
    vec2  w = p-v0;
    vec2  q = w-e*clamp(dot(w,e)/dot(e,e),0.0,1.0);
    float d = dot(q,q);
    float s = gs*cro(w,e);
    res = vec4(d,q,s);
    }
    
    // edge 1
    {
	vec2  e = v2-v1;
    vec2  w = p-v1;
    vec2  q = w-e*clamp(dot(w,e)/dot(e,e),0.0,1.0);
    float d = dot(q,q);
    float s = gs*cro(w,e);
    res = vec4( (d<res.x) ? vec3(d,q) : res.xyz,
                (s>res.w) ?      s    : res.w );
    }
    
    // edge 2
    {
	vec2  e = v0-v2;
    vec2  w = p-v2;
    vec2  q = w-e*clamp(dot(w,e)/dot(e,e),0.0,1.0);
    float d = dot(q,q);
    float s = gs*cro(w,e);
    res = vec4( (d<res.x) ? vec3(d,q) : res.xyz,
                (s>res.w) ?      s    : res.w );
    }
    
    // distance and sign
    float d = sqrt(res.x)*sign(res.w);
    
    return vec3(d,res.yz/d);

}

//=====================================================

void main() {
	vec2 p = (2.0*gl_FragCoord.xy-iResolution.xy)/iResolution.y;
    float px = 2.0/iResolution.y;
	
	vec2 v0 = cos( 0.5*iTime + vec2(0.0,2.0) + 0.0 );
	vec2 v1 = cos( 0.5*iTime + vec2(0.0,1.5) + 1.5 );
	vec2 v2 = cos( 0.5*iTime + vec2(0.0,3.0) + 4.0 );

    // compute traingle SDF
	vec3 dg = sdgTriangle( p, v0, v1, v2 );
    float d = dg.x;
    vec2 g = dg.yz;
    
    // compute triangle equicenter (yellow dot)
    vec2 ce = triangleIncenter( v0, v1, v2 );

    // draw triangle SDF
    vec3 col = (d>0.0) ? vec3(0.9,0.6,0.3) : vec3(1.1,1.4,1.7)*0.5;
    col *= 0.5+vec3(0.5+0.5*g,0.5);
    col *= 1.0 - 0.9*exp(-5.0*abs(d));
    col *= 0.8 + 0.2*cos(200.0*d);
    col = mix( col, vec3(1.0), 1.0-smoothstep(0.0,0.02,abs(d)) );
    col = clamp(col,0.0,1.0);

    // animate equicenter display
    float al = 1.0;//smoothstep(-0.2,0.2,cos(3.1415927*iTime) );
    
    // draw helped bisectors
    col = mix(col,vec3(1.0,1.0,1.0),al*smoothstep(px,0.0,sdSegment( p, v0, ce )-0.005));
    col = mix(col,vec3(1.0,1.0,1.0),al*smoothstep(px,0.0,sdSegment( p, v1, ce )-0.005));
    col = mix(col,vec3(1.0,1.0,1.0),al*smoothstep(px,0.0,sdSegment( p, v2, ce )-0.005));
    
    // draw equicenter in red
    col = mix(col,vec3(1.0,1.0,0.0),al*smoothstep(px,0.0,sdDisk(p,ce,0.025)));

    // output
    fragColor = vec4(col,1.0);
}