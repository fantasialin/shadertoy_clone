// The MIT License
// Copyright © 2025 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

// sdgSphere() returns the sphere SDF and its gradient at the same
// time. This means the normal to the box surface can be used during
// the raymarch loop rather inexpensivelly (compared to sampling the
// SDF multiple times to evaluate a normal for it).
//
// It's the 3D version of https://www.shadertoy.com/view/wlcXD2
//
// List of other 3D distances+gradients/normals:
//   https://iquilezles.org/articles/distgradfunctions3d
// and
//   https://www.shadertoy.com/playlist/cXccR2

// https://www.shadertoy.com/view/wcccW7
#version 330 core
out vec4 fragColor;

uniform vec3 iResolution;   // viewport resolution (in pixels)
uniform float iTime;        // shader playback time (in seconds)
uniform vec4 iMouse;        // xy: current (if MLB down), zw: click

// .x = f(p)
// .y = ∂f(p)/∂x
// .z = ∂f(p)/∂y
// .w = ∂f(p)/∂z
// .yzw = ∇f(p) with ‖∇f(p)‖ = 1
vec4 sdgSphere( in vec3 p, in float r )
{
    float l = length(p);
    return vec4(l-r, p/l);
}

#define AA 3

void main() {
     // camera movement	
	float an = 0.5*(iTime-10.0);
	vec3  ro = 1.4*vec3( 1.0*cos(an), 0.5, 1.0*sin(an) );
    vec3  ta = vec3( 0.0, 0.0, 0.0 );
    // camera matrix
    vec3  ww = normalize( ta - ro );
    vec3  uu = normalize( cross(ww,vec3(0.0,1.0,0.0) ) );
    vec3  vv = normalize( cross(uu,ww));
    // animate box
    float ra = 0.5+0.2*sin(iTime);

    // render    
    vec3 tot = vec3(0.0);
    #if AA>1
    for( int m=0; m<AA; m++ )
    for( int n=0; n<AA; n++ )
    {
        // pixel coordinates
        vec2 o = vec2(float(m),float(n)) / float(AA) - 0.5;
        vec2 p = (2.0*(gl_FragCoord.xy+o)-iResolution.xy)/iResolution.y;
        #else    
        vec2 p = (2.0*gl_FragCoord.xy-iResolution.xy)/iResolution.y;
        #endif

	    // create view ray
        vec3 rd = normalize( p.x*uu + p.y*vv + 2.0*ww );

        // raymarch
        const float tmax = 5.0;
        float t = 0.0;
        for( int i=0; i<256; i++ )
        {
            vec3 pos = ro + t*rd;
            float h = sdgSphere(pos,ra).x;
            //h = max(h,pos.z); // use to see interior gradient
            if( h<0.0001 || t>tmax ) break;
            t += h;
        }
    
        // shading/lighting	
        vec3 col = vec3(0.0);
        if( t<tmax )
        {
            vec3 pos = ro + t*rd;
            vec3 nor = sdgSphere(pos,ra).yzw;

            // numerical normal for comparison (https://iquilezles.org/articles/normalsSDF)
            #if 0
            const vec2 e = vec2(1,-1);
            const float eps = 0.0002;
            nor = normalize( e.xyy*sdgSphere( pos + e.xyy*eps, ra ).x + 
                             e.yyx*sdgSphere( pos + e.yyx*eps, ra ).x + 
                             e.yxy*sdgSphere( pos + e.yxy*eps, ra ).x + 
                             e.xxx*sdgSphere( pos + e.xxx*eps, ra ).x );
            #endif

            float dif = clamp( dot(nor,vec3(0.57703)), 0.0, 1.0 );
            float amb = 0.5 + 0.5*dot(nor,vec3(0.0,1.0,0.0));
            col = vec3(0.2,0.3,0.4)*amb + vec3(0.85,0.75,0.65)*dif;
            col *= (0.5+0.5*nor)*(0.5+0.5*nor);
        }

        // gamma (yes, before accumulation even though light accum is linear)
        col = sqrt( col );
	    tot += col;
    #if AA>1
    }
    tot /= float(AA*AA);
    #endif

	fragColor = vec4( tot, 1.0 );
}