#version 330 core
out vec4 fragColor;

uniform vec3 iResolution;   // viewport resolution (in pixels)
uniform float iTime;        // shader playback time (in seconds)
uniform vec4 iMouse;        // xy: current (if MLB down), zw: click

const int NUMBER_OF_STEPS = 32;
const float MINIMUM_HIT_DISTANCE = 0.001;
const float MAXIMUM_TRACE_DISTANCE = 1000.0;

#define draw_sphere_ 1
#define black_background_ 1

vec3 getBackgroundColor(vec2 uv) {
    uv += 0.5; // remap uv from <-0.5,0.5> to <0,1>
    vec3 gradientStartColor = vec3(1., 0., 1.);
    vec3 gradientEndColor = vec3(0., 1., 1.);
    return mix(gradientStartColor, gradientEndColor, uv.y); // gradient goes from bottom to top
    //return mix(gradientStartColor, gradientEndColor, uv.x); // gradient goes from left to right
}

float distance_from_sphere(in vec3 p, in vec3 c, float r)
{
    return length(p - c) - r;
}

float map_the_world(in vec3 p)
{
    #if draw_sphere_
        float sphere_0 = distance_from_sphere(p, vec3(0.0), 1.0);
        // Later we might have sphere_1, sphere_2, cube_3, etc...
        return sphere_0;
    #else
        float displacement = sin(5.0 * p.x) * sin(5.0 * p.y) * sin(5.0 * p.z) * 0.25;
        float sphere_0 = distance_from_sphere(p, vec3(0.0), 1.0);

        return sphere_0 + displacement;
    #endif
}

vec3 calculate_normal(in vec3 p)
{
    const vec3 small_step = vec3(0.001, 0.0, 0.0);

    float gradient_x = map_the_world(p + small_step.xyy) - map_the_world(p - small_step.xyy);
    float gradient_y = map_the_world(p + small_step.yxy) - map_the_world(p - small_step.yxy);
    float gradient_z = map_the_world(p + small_step.yyx) - map_the_world(p - small_step.yyx);

    vec3 normal = vec3(gradient_x, gradient_y, gradient_z);

    return normalize(normal);
}

vec3 ray_march(vec2 uv, in vec3 ro, in vec3 rd)
{
    #if black_background_
        vec3 col = vec3(0.0);
    #else
        vec3 col = getBackgroundColor(uv);
    #endif
    float total_distance_traveled = 0.0;
    for (int i = 0; i < NUMBER_OF_STEPS; ++i)
    {
        vec3 current_position = ro + total_distance_traveled * rd;
        //float distance_to_closest = distance_from_sphere(current_position, vec3(0.0), 1.0);
        float distance_to_closest = map_the_world(current_position);
        if (distance_to_closest < MINIMUM_HIT_DISTANCE) 
        {
            //return vec3(1.0, 0.0, 0.0);
            vec3 normal = calculate_normal(current_position);

            #if draw_sphere_
                // Remember, each component of the normal will be in 
                // the range -1..1, so for the purposes of visualizing
                // it as an RGB color, let's remap it to the range
                // 0..1
                return normal * 0.5 + 0.5;
            #else

                // For now, hard-code the light's position in our scene
                vec3 light_position = vec3(2.0, -5.0, 3.0);

                // Calculate the unit direction vector that points from
                // the point of intersection to the light source
                vec3 direction_to_light = normalize(current_position - light_position);

                float diffuse_intensity = max(0.0, dot(normal, direction_to_light));

                return vec3(1.0, 0.0, 0.0) * diffuse_intensity;
            #endif
        }
        if (total_distance_traveled > MAXIMUM_TRACE_DISTANCE)
        {
            break;
        }
        total_distance_traveled += distance_to_closest;
    }
    return col;
}

void main() {

    //vec2 uv = gl_FragCoord.xy/iResolution.xy; // <0,1>
    //uv -= 0.5; // <-0.5,0.5>
    //uv.x *= iResolution.x/iResolution.y; // fix aspect ratio
    vec2 uv = (gl_FragCoord.xy - .5 * iResolution.xy) / iResolution.y;   

    vec3 camera_position = vec3(0.0, 0.0, -5.0);
    vec3 ro = camera_position;
    vec3 rd = vec3(uv, 1.0);
    vec3 shaded_color = ray_march(uv, ro, rd);

    // Output to screen
    fragColor = vec4(shaded_color,1.0);
}