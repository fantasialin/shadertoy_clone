// The MIT License
// Copyright Â© 2025 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


// Axis Aligned Bounding Box for a cut disk
//
// More bounding boxes:
//  https://www.shadertoy.com/playlist/mcGBDK
// and
//   https://iquilezles.org/articles/bboxes2d/


// https://www.shadertoy.com/view/t3jfDw
#version 330 core
out vec4 fragColor;

uniform vec3 iResolution;   // viewport resolution (in pixels)
uniform float iTime;        // shader playback time (in seconds)
uniform vec4 iMouse;        // xy: current (if MLB down), zw: click

// r=radius, h=height (-r,r)
vec4 aabbCutDisk( float r, float h )
{
    float m = h>0.0 ? sqrt(r*r-h*h) : r;
    return vec4(-m,h,m,r);
}

// r=radius, h=height (-r,r)
float sdCutDisk( in vec2 p, in float r, in float h )
{
    float w = sqrt(r*r-h*h);
    p.x = abs(p.x);
    float s = max( (h-r)*p.x*p.x+w*w*(h+r-2.0*p.y), h*p.x-w*p.y );
    return (s<0.0) ? length(p)-r :
           (p.x<w) ? h - p.y     :
                     length(p-vec2(w,h));
}

void main() {
    // normalized pixel coordinates
    vec2 p = (2.0*gl_FragCoord.xy-iResolution.xy)/iResolution.y;
    vec2 m = (2.0*iMouse.xy-iResolution.xy)/iResolution.y;
    float px = 2.0/iResolution.y;
    
    // animation
    float ra = 0.75;
    float he = ra*clamp(cos(iTime*0.8),-0.999999,0.999999);

    // distance and bounding box
    float dis = sdCutDisk(p,ra,he);
    vec4  box = aabbCutDisk(ra,he);

    // draw shapes
    vec3 col = (dis>0.0) ? vec3(0.15) : vec3(0.65).zyx;
	col += 0.03*sin(dis*150.0);
	col = mix( col, vec3(1.0), 1.0-smoothstep(0.0,1.5*px,abs(dis)-0.002) );

    // draw bounding box
    {
    vec2 cen = 0.5*(box.zw+box.xy);
    vec2 rad = 0.5*(box.zw-box.xy);
    float d = abs(max(abs(p.x-cen.x)-rad.x,
                      abs(p.y-cen.y)-rad.y))-0.002;
    col = mix(col, vec3(0.2,0.7,1.0), smoothstep(1.5*px,0.0,d));
    }
    
	fragColor = vec4(col, 1.0);
}