// The MIT License
// Copyright Â© 2022 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

// Distance to a spiral made of circular arcs

// List of some other 2D distances: https://www.shadertoy.com/playlist/MXdSRf
//
// and iquilezles.org/articles/distfunctions2d

// https://www.shadertoy.com/view/sssyWN
#version 330 core
out vec4 fragColor;

uniform vec3 iResolution;   // viewport resolution (in pixels)
uniform float iTime;        // shader playback time (in seconds)
uniform vec4 iMouse;        // xy: current (if MLB down), zw: click

float sdSpiral( in vec2 p, float w, in float h )
{
    // base point
    float d = length(p);
    // 8 arcs
    for( int i=0; i<8; i++ )
    {
        p.x -= w;
        if( p.x<0.0 && p.y>0.0 ) d = min( d, abs(length(p)-w) );
        p.y -= w;
        p = vec2(-p.y,p.x);
        w *= h;
    }
    // tip point
    return min( d, length(p) );
}


void main() {
    vec2 p = (2.0*gl_FragCoord.xy-iResolution.xy)/iResolution.y;
    vec2 m = (2.0*iMouse.xy-iResolution.xy)/iResolution.y;
    float px = 2.0/iResolution.y;
    
    
    float w = 1.0;
    float h = 0.5+0.5*sin((iTime-10.0)*0.3+2.90327519);
    //float h = (sqrt(5.0)-1.0)/2.0; // approximated golden spiral
    
    p  *= 1.15;
    px *= 1.15;
    
    p -= vec2(-0.8,-0.3);
    m -= vec2(-0.8,-0.3);

    float d = sdSpiral(p,w,h);

   
    vec3 col = (d>0.0) ? vec3(0.9,0.6,0.3) : vec3(0.65,0.85,1.0);
    col *= 1.0 - exp(-7.0*abs(d));
    col *= 0.8 + 0.2*cos(160.0*abs(d));
    col = mix( col, vec3(1.0), 1.0-smoothstep(-px,px,abs(d)-0.005) );


    if( iMouse.z>0.001 )
    {
    d = sdSpiral(m,w,h);
    col = mix(col, vec3(1.0,1.0,0.0), 1.0-smoothstep(0.0, 0.005, abs(length(p-m)-abs(d))-0.0025));
    col = mix(col, vec3(1.0,1.0,0.0), 1.0-smoothstep(0.0, 0.005, length(p-m)-0.015));
    }
    
    fragColor = vec4(col,1.0);
}